agent Robot{
    module EIS ei;
    module Console C;
    module System S;
    module MyModule mod;

    types robot {
        formula id(string);
        formula nextDirection(string);
        formula location(int, int);
    }

    rule @message(request, string sender, nextDirection(string X)) {
        ei.move(X, 1);
        while(~ei.task("none")){
            S.sleep(500);
        }
    }

    rule +!main([string name, string id]){
        ei.launch("vw","dependency/vacuumworld-1.2.0.jar");
        ei.join();
        ei.link(name);
        +id(id);
        C.println("name: " + name);
    }

    rule +$ei.event(location(int x, int y)) : id(string id) & ei.direction(string facing){
        C.println("col: " + x + ", row: " + y);
        string col = "" + x;
        string row = "" + y;
        mod.updateStep(row, col, id);
        foreach(ei.square(string squareName, string squareContents)){
            if(squareContents == "dust" | squareContents == "empty"){
                mod.updateNonObstacle(row, col, squareName, facing);
            }
        }
    
        ei.clean();
        
        while(~ei.task("none")){
            S.sleep(500);
        }

        send(request, "main", location(x, y));
	}

    
}